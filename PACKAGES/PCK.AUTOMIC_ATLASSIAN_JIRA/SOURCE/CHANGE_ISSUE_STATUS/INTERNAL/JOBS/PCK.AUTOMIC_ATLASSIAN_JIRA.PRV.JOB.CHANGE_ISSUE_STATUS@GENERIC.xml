<?xml version="1.0" encoding="UTF-8"?>
<jobs>
   <metadata>
      <row>
         <version>21.0.0</version>
      </row>
   </metadata>
   <general_attributes>
      <row>
         <minimum_ae_version>11.2</minimum_ae_version>
         <auto_deactivation>A</auto_deactivation>
         <child_flags>00000000000000000000000000000000</child_flags>
         <platform>GENERIC</platform>
         <last_runtimes>AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA</last_runtimes>
         <max_parallel_action>1</max_parallel_action>
         <mrt_time>000000</mrt_time>
         <name>PCK.AUTOMIC_ATLASSIAN_JIRA.PRV.JOB.CHANGE_ISSUE_STATUS@GENERIC</name>
         <type>JOBS</type>
         <has_object_variables>1</has_object_variables>
         <inherit_output_filter>N</inherit_output_filter>
         <queue>CLIENT_QUEUE</queue>
         <description>Change status of an Issue in Jira</description>
         <versioning_id>-2042106598</versioning_id>
      </row>
   </general_attributes>
   <scripts>
      <row>
         <process><![CDATA[: IF &$PLATFORM# EQ "WINDOWS"
    @echo off
: ENDIF

!-------------------------------------Logging input parameters-------------------------------------------------------------------------------------------------
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.HEADER.PARAM_VALIDATION
: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.COMMON_LOGGING
: SET &UC4RB_LOGGING_PARAMETER# = "Issue ID or Key"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_JIRA_ISSUE_ID_KEY#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM
: SET &UC4RB_LOGGING_PARAMETER# = "Target Status"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_JIRA_TARGET_STATUS#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM
: SET &UC4RB_LOGGING_PARAMETER# = "Resolution"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_JIRA_RESOLUTION#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM
: SET &UC4RB_LOGGING_PARAMETER# = "Assignee"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_JIRA_ASSIGNEE#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM
: SET &UC4RB_LOGGING_PARAMETER# = "Comment"
: SET &UC4RB_LOGGING_PARAMETER_VAL# = "&UC4RB_JIRA_COMMENT#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.PARAM

!------------------------------------------ Buidling java arguments --------------------------------------------------------------------------------------------

: INC PCK.ITPA_SHARED.PRV.INCLUDE.LOGGING.HEADER.EXECUTION

: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.ATTACH_RESOURCE

! Define action specific java options and values
: SET &ACTION_NAME# = "ChangeIssueStatusAction"

! Build java arguments
: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.PREPARE_CMD

! Issue id or key
: SET &UC4RB_JIRA_ESCAPED_STRING# = &UC4RB_JIRA_ISSUE_ID_KEY#
: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.ESCAPE_ARG_VALUE
: SET &UC4RB_JIRA_CMD# = STR_CAT(&UC4RB_JIRA_CMD#, " -issueid &UC4RB_JIRA_ESCAPED_STRING#")


! Target status of Issue
: SET &UC4RB_JIRA_ESCAPED_STRING# = &UC4RB_JIRA_TARGET_STATUS#
: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.ESCAPE_ARG_VALUE
: SET &UC4RB_JIRA_CMD# = STR_CAT(&UC4RB_JIRA_CMD#, " -targetstatus &UC4RB_JIRA_ESCAPED_STRING#")

!--------------------------------------------------Building json file contents------------------------------------------------

:    SET &SEPARATOR# = ' '
:    SET &UC4RB_JSON_CONTENTS# =""
:    SET &UC4RB_FILE_CONTENT# = '{ "transition": { "id": "transition_id" }'


! Resolution
:   IF &UC4RB_JIRA_RESOLUTION# NE ""
:           SET &UC4RB_PARAM_VALUE# = &UC4RB_JIRA_RESOLUTION#
:           INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_JSON_OBJECT &UC4RB_PARAM_NAME# = 'name'
:           INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_JSON &UC4RB_FIELD_NAME# = 'resolution'
:   ENDIF

! Assignee
:    IF &UC4RB_JIRA_ASSIGNEE# NE ""
:            SET &UC4RB_PARAM_VALUE# = &UC4RB_JIRA_ASSIGNEE#
:            INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_JSON_OBJECT &UC4RB_PARAM_NAME# = 'name'
:            INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_JSON &UC4RB_FIELD_NAME# = 'assignee'
:    ENDIF

! Custom fields
:    IF &UC4RB_JIRA_CUSTOM_FIELDS# NE ""
:            SET &UC4RB_JSON_CONTENTS# = STR_CAT(' &UC4RB_JSON_CONTENTS#',' , ')
:            SET &UC4RB_JSON_CONTENTS# = STR_CAT('&UC4RB_JSON_CONTENTS# ','&UC4RB_JIRA_CUSTOM_FIELDS#')
:    ENDIF

!Checking Json contents and if not null add it to the file contents as proper json
:    IF &UC4RB_JSON_CONTENTS# NE ""
:            SET &UC4RB_JSON_CONTENTS# = STR_CAT('"fields":','{&UC4RB_JSON_CONTENTS#}')
:            SET &UC4RB_FILE_CONTENT# = STR_CAT(&UC4RB_FILE_CONTENT#,',')
:            SET &UC4RB_FILE_CONTENT# = STR_CAT(&UC4RB_FILE_CONTENT#,&UC4RB_JSON_CONTENTS#)
:    ENDIF


! Add comment
:   IF &UC4RB_JIRA_COMMENT# NE ""
:            SET &UC4RB_PARAM_VALUE# = &UC4RB_JIRA_COMMENT#
:            INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_JSON_OBJECT &UC4RB_PARAM_NAME# = 'body'
:            SET &UC4RB_PARAM_VALUE# = &UC4RB_FIELD_VALUE#
:            SET &UC4RB_FIELD_VALUE# = STR_CAT('"add":','&UC4RB_FIELD_VALUE#')
:            SET &UC4RB_FIELD_VALUE# = STR_CAT('"comment":','[{&UC4RB_FIELD_VALUE#}]')
:            SET &UC4RB_FILE_CONTENT# = STR_CAT(&UC4RB_FILE_CONTENT#,',')
:            SET &UC4RB_FILE_CONTENT# = STR_CAT(&UC4RB_FILE_CONTENT#,'"update":{&UC4RB_FIELD_VALUE#}')
:   ENDIF

!Closing paranthesis
:          SET &UC4RB_FILE_CONTENT# = STR_CAT(&UC4RB_FILE_CONTENT#,'}')
!----------------------------------------------Building json file contents (end)----------------------------------------------------

! write to a file
: SET &AGENT_CURR_DIR# = GET_VAR('UC_EX_PATH_TEMP', &AGENT#)
: SET &UC4RB_FILE_PATH# = STR_CAT(&AGENT_CURR_DIR#,"changeissuestatus-&$RUNID#.json")
: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CREATE_FILE
: SET &DELETEFILE# = 1

: PUBLISH &UC4RB_FILE_PATH#,,"TASK"
: SET &UC4RB_JIRA_CMD# = STR_CAT(&UC4RB_JIRA_CMD#, " -jsonfilepath &UC4RB_FILE_PATH# ")

: PRINT &UC4RB_JIRA_CMD#

! Access key is decrypted and send to jar as argument
: INC PCK.ITPA_SHARED.PUB.INCLUDE.ATTACH
: SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_JIRA_PASSWORD#"
: INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD


! Set Execution Environment
: INC PCK.ITPA_SHARED.PRV.INCLUDE.CHANGE_DIRECTORY_TO_AGENT_BIN


! Invoking java binary
: SWITCH &$PLATFORM#
:   CASE "WINDOWS"
           &UC4RB_JIRA_CMD# -password "%UC4_DECRYPTED_PWD%"
:   CASE "UNIX"
           &UC4RB_JIRA_CMD# -password "$UC4_DECRYPTED_PWD"
:   OTHER
: ENDSWITCH


! Status messages
: SET &UC4RB_LOGGING_INFO# = "******** CHANGE ISSUE STATUS ACTION EXECUTED SUCCESSFULLY ********"
: SET &UC4RB_LOGGING_ERROR# = "******** CHANGE ISSUE STATUS ACTION FAILED ********"

!------------------------------------------ Error Handling -----------------------------------------------------------------------------------------------------

: INC PCK.AUTOMIC_ATLASSIAN_JIRA.PUB.INCLUDE.CHECK_JOBSTATUS]]></process>
      </row>
      <row>
         <pre_process>:INC PCK.ITPA_SHARED.PUB.INCLUDE.START_STOP_JOB_ACCORDING_TO_AGENT_OS</pre_process>
      </row>
   </scripts>
   <job_attributes>
      <row>
         <platform>GENERIC</platform>
         <agent><![CDATA[<GENERIC>]]></agent>
      </row>
   </job_attributes>
   <object_variables>
      <row>
         <ert_usage>2</ert_usage>
         <value>0</value>
         <name>&amp;DELETEFILE#</name>
      </row>
   </object_variables>
   <rollback_definitions>
      <row>
      </row>
   </rollback_definitions>
</jobs>
