<?xml version="1.0" encoding="UTF-8"?>
<jobi>
   <metadata>
      <row>
         <version>21.0.0</version>
      </row>
   </metadata>
   <general_attributes>
      <row>
         <minimum_ae_version>11.2</minimum_ae_version>
         <child_flags>00000000000000000000000000000000</child_flags>
         <last_runtimes>AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA</last_runtimes>
         <name>PCK.ITPA_SHARED.PUB.INCLUDE.SSH_CONNECTION_ARGS</name>
         <type>JOBI</type>
         <description>Prepares connection arguments</description>
         <versioning_id>-1577111791</versioning_id>
      </row>
   </general_attributes>
   <scripts>
      <row>
         <process><![CDATA[: SET &UC4RB_SSH_CONNARGS# = ""

!SSH Host
: SET &INPUT_SSHHOST# = &UC4RB_SSH_HOST#
: SET &INPUT_SSHKEY# = &UC4RB_SSH_KEY#
: SET &INPUT_SSHUNAME# = &UC4RB_SSH_USERNAME#
: IF &UC4RB_AGENT_OS# = "WINDOWS"
:   IF &UC_WIN_TYP = "EXTCOMPROC"
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@POWERSHELL &UC4RB_ESCAPED_STRING# = &INPUT_SSHHOST#
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@POWERSHELL &UC4RB_ESCAPED_STRING# = &INPUT_SSHKEY#
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@POWERSHELL &UC4RB_ESCAPED_STRING# = &INPUT_SSHUNAME#
:   ELSE
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@WINDOWS &UC4RB_ESCAPED_STRING# = &INPUT_SSHHOST#
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@WINDOWS &UC4RB_ESCAPED_STRING# = &INPUT_SSHKEY#
:      INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@WINDOWS &UC4RB_ESCAPED_STRING# = &INPUT_SSHUNAME#
:   ENDIF
: ELSE
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@UNIX &UC4RB_ESCAPED_STRING# = &INPUT_SSHHOST#
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@UNIX &UC4RB_ESCAPED_STRING# = &INPUT_SSHKEY#
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.ESCAPE_CHARS@UNIX &UC4RB_ESCAPED_STRING# = &INPUT_SSHUNAME#
: ENDIF

: SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, ' -sshhost "&INPUT_SSHHOST#"')

!SSH Port
: SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, " -sshport &UC4RB_SSH_PORT#")

!Connection Timeout
: SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, " -conntimeout &UC4RB_SSH_CONN_TIMEOUT#")

!SSH Username
: SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, ' -username "&INPUT_SSHUNAME#"')

: SET &SUDO_TMP# = ""
!SSH Options
: IF &UC4RB_SSH_OPTIONS# <> ""
!  Adding Strict host Key Check in command
:  SET &POS# = STR_FIND(&UC4RB_SSH_OPTIONS#,"Strict Host Key Check")
:  IF &POS# <> 0
:    SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, " -hostkeycheck")
:  ENDIF
: ENDIF

!SSH Password
: IF &UC4RB_SSH_PASSWORD# NE "" OR "--10"
:   SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_SSH_PASSWORD#"
:   INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
:   IF &UC4RB_AGENT_OS# = "WINDOWS"
:     IF &UC_WIN_TYP = "EXTCOMPROC"
        $env:UC4_DECRYPTED_SSHPWD=$UC4_DECRYPTED_PWD
:     ELSE
        set UC4_DECRYPTED_SSHPWD=%UC4_DECRYPTED_PWD%
:     ENDIF
:   ELSE
        export UC4_DECRYPTED_SSHPWD=$UC4_DECRYPTED_PWD
:   ENDIF
: ELSE
:   IF &UC4RB_SSH_KEY# NE ""
:     SET &UC4RB_SSH_CONNARGS# = STR_CAT(&UC4RB_SSH_CONNARGS#, ' -sshkey "&INPUT_SSHKEY#"')
:     IF &UC4RB_SSH_PASSPHRASE# NE "" OR "--10"
:       SET &UC4RB_ENCRYPTED_PASSWORD_TMP# = "&UC4RB_SSH_PASSPHRASE#"
!       Decrypt passphrase
:       INC PCK.ITPA_SHARED.PRV.INCLUDE.DECRYPT_PASSWORD
:       IF &UC4RB_AGENT_OS# = "WINDOWS"
:         IF &UC_WIN_TYP = "EXTCOMPROC"
            $env:UC4_DECRYPTED_PASSPHRASE=$UC4_DECRYPTED_PWD
:         ELSE
            set UC4_DECRYPTED_PASSPHRASE=%UC4_DECRYPTED_PWD%
:         ENDIF
:       ELSE
            export UC4_DECRYPTED_PASSPHRASE=$UC4_DECRYPTED_PWD
:       ENDIF
:     ENDIF
:   ENDIF
: ENDIF]]></process>
      </row>
   </scripts>
</jobi>
